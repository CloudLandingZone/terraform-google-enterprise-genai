#### Resources related to Cloud Composer in the Operational environments ####

# Composer Service Account
resource "google_service_account" "composer" {
  account_id   = "composer"
  display_name = "${title(var.environment)} Composer Service Account"
  description  = "Service account to be used by Cloud Composer"
  project      = data.google_project.project.project_id
}

resource "google_project_iam_member" "composer_worker_composer_service_account" {
  project = data.google_project.project.project_id
  role    = "roles/composer.worker"
  member  = "serviceAccount:${google_service_account.composer.email}"
}

# Pubsub
resource "google_project_service_identity" "pubsub" {
  provider = google-beta

  project = data.google_project.project.project_id
  service = "pubsub.googleapis.com"
}

resource "google_project_iam_member" "pubsub_agent_kms" {
  project = data.google_project.project.project_id
  role    = "roles/cloudkms.cryptoKeyEncrypterDecrypter"
  member  = "serviceAccount:${google_project_service_identity.pubsub.email}"
}

# GKE
resource "google_project_service_identity" "gke" {
  provider = google-beta

  project = data.google_project.project.project_id
  service = "container.googleapis.com"
}

resource "google_project_iam_member" "gke_agent_kms" {
  project = data.google_project.project.project_id
  role    = "roles/cloudkms.cryptoKeyEncrypterDecrypter"
  member  = "serviceAccount:${google_project_service_identity.gke.email}"
}

# Compute engine
resource "google_project_iam_member" "compute_agent_kms" {
  project = data.google_project.project.project_id
  role    = "roles/cloudkms.cryptoKeyEncrypterDecrypter"
  member  = "serviceAccount:service-${data.google_project.project.number}@compute-system.iam.gserviceaccount.com"
}

# Big Query
resource "google_project_iam_member" "composer_bigquery" {
  project = data.google_project.project.project_id
  role    = google_project_iam_custom_role.composer-sa-bq.id
  member  = format("serviceAccount:%s", google_service_account.composer.email)
}

# Vertex AI
resource "google_project_iam_member" "composer_vertex" {
  project = data.google_project.project.project_id
  role    = google_project_iam_custom_role.composer-sa-vertex.id
  member  = format("serviceAccount:%s", google_service_account.composer.email)
}
